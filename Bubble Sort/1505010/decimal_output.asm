OUTDEC PROC
PUSH AX
PUSH BX
PUSH CX
PUSH DX
;if AX < 0
OR AX, AX ; AX<0
JGE END_IFI ;NO, >0
;THEN

PUSH AX ; SAVE NUMBER
MOV DL, '-' ;GET '-'
MOV AH, 2 ; PRINT CHAR FUNCTION
INT 21H ; PRINT '-'
POP AX ; GET AX BACK
NEG AX ; AX = -AX

END_IFI:
;GET DECIMAL DIGITS
XOR CX, CX ; CX COUNTS DIGITS
MOV BX, 10D ; BX HAS DIVISOR
REPEAT1:
XOR DX, DX ; PREPARE HIGH WORD OF DIVIDEND
DIV BX ; AX = QUOTIENT, DX = REMAINDER
PUSH DX ; SAVE REMAINDER ON STACK
INC CX ; COUNT=COUNT+1
;UNTIL
OR AX, AX ; QUOTIENT = 0?
JNE REPEAT1 ;NO, KEEP GOING
;CONVERT DIGITS TO CHARS AND PRINT
MOV AH, 2 ; PRINT CHAR FUNC
; FOR COUNT TIMES DO
PRINT_LOOP:
POP DX ; DIGITS IN DL
OR DL, 30H ; CONVERT TO CHAR
INT 21H ; PRINT DIGIT
LOOP PRINT_LOOP
;END FOR
POP DX
POP CX
POP BX
POP AX
RET
OUTDEC ENDP